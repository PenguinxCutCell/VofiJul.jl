using Test
using VofiJul

@test vofi_real === Float64
@test NDIM == 3 == length(MinData().xval)

nodes3 = gauss_legendre_nodes(3)
weights4 = gauss_legendre_weights(4)
@test length(nodes3) == 3
@test nodes3[1] ≈ -0.7745966692414833
@test sum(weights4) ≈ 2.0

len = LenData()
@test length(len.xt0) == NGLM + 2

function plane_func(x, _)
    return x[1] + x[2] - 0.5
end

function neg_func(x, _)
    return -1.0
end

function pos_func(x, _)
    return 1.0
end

# Test 1D functionality
@test vofi_get_cell_type(neg_func, nothing, [0.0], [1.0], 1) == 1
@test vofi_get_cell_type(pos_func, nothing, [0.0], [1.0], 1) == 0

function line_func_1d(x, _)
    return x[1] - 0.25
end
@test vofi_get_cell_type(line_func_1d, nothing, [0.0], [1.0], 1) == -1

xex_1d = zeros(Float64, 4)
cc_full_1d = vofi_get_cc(neg_func, nothing, [0.0], [1.0], xex_1d,
                        [0, 0], [0, 0], [0, 0], 1)
@test cc_full_1d ≈ 1.0

xex_1d_cut = zeros(Float64, 4)
cc_cut_1d = vofi_get_cc(line_func_1d, nothing, [0.0], [1.0], xex_1d_cut,
                       [1, 0], [0, 0], [0, 0], 1)
@test cc_cut_1d ≈ 0.25 atol=1e-6
@test xex_1d_cut[1] ≈ 0.125 atol=1e-6

# Test 1D integration over multiple cells (similar to 2D circle test)
function line_sdf(x, _)
    r = 0.4
    return abs(x[1]) - r
end

let
    n = 20
    h = 1.0 / n
    xmin = -0.5
    total_length = 0.0
    cell_length = h
    xex_tmp = zeros(Float64, 4)
    for i in 0:n-1
        xin = [xmin + i * h]
        cc = vofi_get_cc(line_sdf, nothing, xin, [h], xex_tmp,
                         [0, 0], [0, 0], [0, 0], 1)
        total_length += cc * cell_length
    end
    exact = 2 * 0.4  # Length of interval [-0.4, 0.4]
    @test total_length ≈ exact atol=1e-2
end

# Test 2D functionality
@test vofi_get_cell_type(neg_func, nothing, [0.0, 0.0], [1.0, 1.0], 2) == 1
@test vofi_get_cell_type(pos_func, nothing, [0.0, 0.0, 0.0], [1.0, 1.0, 1.0], 3) == 0
@test vofi_get_cell_type(plane_func, nothing, [0.0, 0.0], [1.0, 1.0], 2) == -1

xex = zeros(Float64, 4)
cc_full = vofi_get_cc(neg_func, nothing, [0.0, 0.0], [1.0, 1.0], xex,
                      [0, 0], [0, 0], [0, 0], 2)
@test cc_full ≈ 1.0


function slanted_func(x, _)
    return 0.25 - x[1]
end

xex_cut2 = zeros(Float64, 4)
cc_cut2 = vofi_get_cc(slanted_func, nothing, [0.0, 0.0], [1.0, 1.0], xex_cut2,
                      [1, 1], [0, 0], [0, 0], 2)
@test cc_cut2 ≈ 0.75 atol=1e-6
@test xex_cut2[4] > 0.0

xex3 = zeros(Float64, 4)
cc_full3d = vofi_get_cc(neg_func, nothing, [0.0, 0.0, 0.0], [1.0, 1.0, 1.0], xex3,
                        [1, 0], [0, 0, 0, 0], [0, 0], 3)
@test cc_full3d ≈ 1.0
@test xex3[1:3] ≈ [0.5, 0.5, 0.5]

xex3_empty = zeros(Float64, 4)
cc_empty3d = vofi_get_cc(pos_func, nothing, [0.0, 0.0, 0.0], [1.0, 1.0, 1.0],
                         xex3_empty, [0, 0], [0, 0, 0, 0], [0, 0], 3)
@test cc_empty3d ≈ 0.0

function plane_func3d(x, _)
    return x[1] + x[2] + x[3] - 1.5
end

xex3_plane = zeros(Float64, 4)
cc_plane = vofi_get_cc(plane_func3d, nothing, [0.0, 0.0, 0.0], [1.0, 1.0, 1.0],
                       xex3_plane, [0, 0], [0, 0, 0, 0], [0, 0], 3)
@test cc_plane ≈ 0.5 atol=1e-3

# simple Cartesian integration of a sphere volume
function sphere_sdf(x, _)
    r = 0.4
    return sqrt((x[1])^2 + (x[2])^2 + (x[3])^2) - r
end

let
    n = 20
    h = 1.0 / n
    xmin = -0.5
    total_vol = 0.0
    cell_vol = h^3
    xex_tmp = zeros(Float64, 4)
    for i in 0:n-1, j in 0:n-1, k in 0:n-1
        xin = [xmin + i * h, xmin + j * h, xmin + k * h]
        cc = vofi_get_cc(sphere_sdf, nothing, xin, [h, h, h], xex_tmp,
                         [0, 0], [0, 0, 0, 0], [0, 0], 3)
        total_vol += cc * cell_vol
    end
    exact = 4 / 3 * π * 0.4^3
    @test total_vol ≈ exact atol=2e-2
end

# simple Cartesian integration of a circle area
function circle_sdf(x, _)
    r = 0.4
    return sqrt((x[1])^2 + (x[2])^2) - r
end

let
    n = 20
    h = 1.0 / n
    xmin = -0.5
    total_area = 0.0
    cell_area = h^2
    xex_tmp = zeros(Float64, 4)
    for i in 0:n-1, j in 0:n-1
        xin = [xmin + i * h, xmin + j * h]
        cc = vofi_get_cc(circle_sdf, nothing, xin, [h, h], xex_tmp,
                         [0, 0], [0, 0, 0, 0], [0, 0], 2)
        total_area += cc * cell_area
    end
    exact = π * 0.4^2
    @test total_area ≈ exact atol=2e-2
end

# -----------------------------

@testset "2D Vofi test" begin
    include("vofi_test_2d.jl")
end

# -----------------------------
@testset "3D Vofi test" begin
    include("vofi_test_3d.jl")
end

# -----------------------------
@testset "4D Vofi test" begin
    include("vofi_test_4d.jl")
end
# Test 4D functionality
function neg_func_4d(x, _)
    return -1.0
end

function pos_func_4d(x, _)
    return 1.0
end

function hyperplane_func_4d(x, _)
    return x[1] + x[2] + x[3] + x[4] - 2.0
end

@test vofi_get_cell_type(neg_func_4d, nothing, [0.0, 0.0, 0.0, 0.0], [1.0, 1.0, 1.0, 1.0], 4) == 1
@test vofi_get_cell_type(pos_func_4d, nothing, [0.0, 0.0, 0.0, 0.0], [1.0, 1.0, 1.0, 1.0], 4) == 0
@test vofi_get_cell_type(hyperplane_func_4d, nothing, [0.0, 0.0, 0.0, 0.0], [1.0, 1.0, 1.0, 1.0], 4) == -1

# -----------------------------

@testset "vofi_get_cell_type 1D/2D/3D/4D" begin
    include("vofi_cell_type_tests.jl")
end

@testset "vofi_interface_centroid tests" begin
    include("interface_centroid_test.jl")
end

# Threaded tests
include("threaded_test.jl")